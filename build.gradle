plugins {
    id 'cpp'
    id 'c'
    id 'google-test'
}

model {
    platforms {
        linux_x86_64 {
            architecture "x86_64"
            operatingSystem "linux"
        }
        cygwin_x86_64 {
            architecture "x86_64"
            operatingSystem "windows"
        }
    }
    buildTypes {
        debug
        if (project.hasProperty("full")) {
            release
        }
    }
    toolChains {
        gcc(Gcc) {
            target("linux_x86_64") {
                cppCompiler.executable = "${gcc_toolchain}/bin/g++"
                cCompiler.executable = "${gcc_toolchain}/bin/gcc"
                linker.executable = "${gcc_toolchain}/bin/g++"
                staticLibArchiver.executable = "/usr/bin/ar"
                cppCompiler.withArguments { args ->
                    args << "-std=c++17"
                    args << "-fPIC"
                }
                linker.withArguments { args ->
                    args << "-pthread"
                }
            }
        }
    }
    repositories {
        libs(PrebuiltLibraries) {
            googleTest {
                binaries.withType(StaticLibraryBinary) {
                    headers.srcDir '/usr/local/googleTest/include'
                    staticLibraryFile = file("/usr/local/googleTest/lib/libgtest.a")
                }
            }
            googleTestMain {
                binaries.withType(StaticLibraryBinary) {
                    headers.srcDir '/usr/local/googleTest/include'
                    staticLibraryFile = file("/usr/local/googleTest/lib/libgtest_main.a")
                }
            }
        }
    }
    binaries {
        all {
            if (buildType == buildTypes.debug) {
                cppCompiler.define "CBI_CHECKPOINTS=1"
                cCompiler.define "CBI_CHECKPOINTS=1"
                cppCompiler.args "-Wno-deprecated-declarations", "-g", "-O0", "-Werror=return-type", "-fPIC"
                linker.args "-g"
            } else {
                cppCompiler.args "-Wno-deprecated-declarations", "-O2", "-Werror=return-type", "-fPIC"
            }
        }
        withType(SharedLibraryBinary) {
            if (buildType == buildTypes.debug) {
                sharedLibraryFile = file("${project.buildDir}/libs/libCBIUtil-d.so")
            } else {
                sharedLibraryFile = file("${project.buildDir}/libs/libCBIUtil.so")
            }
        }
        withType(StaticLibraryBinary) {
            if (buildType == buildTypes.debug) {
                staticLibraryFile = file("${project.buildDir}/libs/libCBIUtil-d.a")
            } else {
                staticLibraryFile = file("${project.buildDir}/libs/libCBIUtil.a")
            }
        }
    }
    components {
        CBIUtil(NativeLibrarySpec) {
            targetPlatform "linux_x86_64"
            sources {
                cpp {
                    lib library: "CBIUtil", linkage: "static"
                    source {
                        srcDir 'src/CompuBrite'
                        include "*.cc"
                        include "*.cpp"
                    }
                    exportedHeaders {
                        srcDirs 'include'
                    }
                }
            }
        }
    }
    testSuites {
        CBIUtilTest {
            sources {
                cpp {
                    lib library: "googleTest", linkage: "static"
                    lib library: "googleTestMain", linkage: "static"
                    lib library: "CBIUtil", linkage: "static"
                    source {
                        srcDir "test"
                        include "*.cc"
                        include "*.cpp"
                    }
                    binaries.all {
                        cppCompiler.define "GOOGLE_TEST"
                        cCompiler.define "GOOGLE_TEST"
                    }
                }
            }
        }
    }
}

// Custom installDist task
task installDist {
    dependsOn assemble

    doLast {
        // Get prefix from gradle.properties
        def installPrefix = project.hasProperty('prefix') ? project.property('prefix') : '/usr/local'

        println "Installing to prefix: ${installPrefix}"

        // Create directories
        def includeDir = file("${installPrefix}/include")
        def libDir = file("${installPrefix}/lib64")

        includeDir.mkdirs()
        libDir.mkdirs()

        // Copy header files
        copy {
            from 'include'
            into includeDir
            include '**/*.h'
            include '**/*.hpp'
        }
        println "Installed headers to: ${includeDir}"

        // Install libraries by finding the built artifacts and renaming them properly
        def buildLibsDir = file("${project.buildDir}/libs/cBIUtil")

        // Install static libraries
        def debugStaticLib = file("${buildLibsDir}/static/debug/libCBIUtil.a")
        def releaseStaticLib = file("${buildLibsDir}/static/release/libCBIUtil.a")

        if (debugStaticLib.exists()) {
            copy {
                from debugStaticLib
                into libDir
                rename { "libCBIUtil-d.a" }
            }
            println "Installed: ${libDir}/libCBIUtil-d.a"
        }

        if (releaseStaticLib.exists()) {
            copy {
                from releaseStaticLib
                into libDir
                rename { "libCBIUtil.a" }
            }
            println "Installed: ${libDir}/libCBIUtil.a"
        }

        // Install shared libraries
        def debugSharedLib = file("${buildLibsDir}/shared/debug/libCBIUtil.so")
        def releaseSharedLib = file("${buildLibsDir}/shared/release/libCBIUtil.so")

        if (debugSharedLib.exists()) {
            copy {
                from debugSharedLib
                into libDir
                rename { "libCBIUtil-d.so" }
            }
            println "Installed: ${libDir}/libCBIUtil-d.so"
        }

        if (releaseSharedLib.exists()) {
            copy {
                from releaseSharedLib
                into libDir
                rename { "libCBIUtil.so" }
            }
            println "Installed: ${libDir}/libCBIUtil.so"
        }

        println "Installation completed to ${installPrefix}"
        println "Note: Installation to system directories may require elevated permissions"
    }
}

// Add description for the task
installDist.description = 'Install headers and libraries with correct naming to specified prefix'
installDist.group = 'distribution'
